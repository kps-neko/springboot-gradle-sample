buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.7.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'springboot-gradle-sample'
    version =  '0.1.0'

    // 依存jar内のクラスも含めて一つのjarに入れるための設定
    //from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

repositories {
    mavenCentral()
}

// 拡張プロパティ
ext {
  // loggers
  slf4jVersion = "1.7.9"
  logbackVersion = "1.1.2"

  // spring boot plugin
  springBootVersion = "1.2.1.RELEASE"

  // tomcat
  tomcatVersion = "8.0.15"

  // Tthymeleaf
  thymeleafVersion = "2.1.4.RELEASE"

  // postgresql
  postgresqlVersion = "9.4-1204-jdbc4"

  // MyBatis
  myBatisVersion = "3.2.8"
  mybatisSpringVersion = "1.2.2"
  mybatisGeneratorVersion = "1.3.2"

  // springSecurity
  springSecurityVersion = "3.2.6.RELEASE"
}

// Java Use Version
sourceCompatibility = 1.8
targetCompatibility = 1.8

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

// mybatisタスクの設定を追加。
configurations {
    myBatisGenerator
}

// 依存関係設定
dependencies {
  // loggers
  compile "org.slf4j:slf4j-api:$slf4jVersion"
  compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
  compile "ch.qos.logback:logback-classic:$logbackVersion"

  // spring-boot
  compile "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
  compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
  compile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
  compile "org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion"
  compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
  compile "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"

  // tomcat-for read JSP
  compile "org.apache.tomcat.embed:tomcat-embed-jasper:$tomcatVersion"

  // Thymeleaf
  compile "org.thymeleaf:thymeleaf-spring4:$thymeleafVersion"
  // ロールによって表示項目の制御するために必要
  compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity3:2.1.2.RELEASE'

  // postgresql
  compile "org.postgresql:postgresql:$postgresqlVersion"

  // MyBatis
  compile "org.mybatis:mybatis:$myBatisVersion"
  compile "org.mybatis:mybatis-spring:$mybatisSpringVersion"
  // compile "org.mybatis.generator:mybatis-generator-core:$mybatisGeneratorVersion"

  // spring-security
  compile "org.springframework.security:spring-security-web:$springSecurityVersion"
  compile "org.springframework.security:spring-security-config:$springSecurityVersion"

  // mybatisタスクの設定に依存ライブラリを追加する。
  myBatisGenerator "org.mybatis.generator:mybatis-generator-core:$mybatisGeneratorVersion"
  myBatisGenerator "org.postgresql:postgresql:$postgresqlVersion"

  testCompile "junit:junit"
}

// mybatisタスクはMyBatisGeneratorを実行する。
task mybatis << {
    ant.taskdef(
            name: 'genfiles',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.myBatisGenerator.asPath)
    ant.genfiles(overwrite: 'true', configfile : "$projectDir/src/main/resources/generatorConfig.xml" , verbose: 'false')
}